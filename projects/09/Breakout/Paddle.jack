class Paddle 
{
    // The game the paddle's a part of
    field Breakout  game;

    // The screen location
    field int x, y;

    // The width and height
    field int width, height;

    // The direction of the bat's movement
    field int direction; // 1 = left, 2 = right, 0 = stationary
    
    constructor Paddle new(int Ax, int Ay, int Awidth, int Aheight, Breakout Agame) 
    {
	let x = Ax;
	let y = Ay;
	let width = Awidth - 1;
	let height = Aheight - 1;
	let direction = 0;
        let game = Agame;

        do show();

        return this;
    }

    method void dispose() 
    {
        do Memory.deAlloc(this);

        return;
    }

    method void show() 
    {
        do Screen.setColor(true);
	do draw();

        return;
    }

    method void hide() 
    {
        do Screen.setColor(false);
	do draw();

        return;
    }

    // Draws the paddle on screen
    method void draw() 
    {
	do Screen.drawRectangle(x, y, x + width, y + height);

	return;
    }

    // Returns left edge of the paddle
    method int getLeft() 
    {
        return x;
    }

    // Returns right edge of the paddle
    method int getRight() 
    {
        return x + width;
    }

    // Move the paddle one step in the given direction if possible
    method void move(int Adirection) 
    {
        var int leftBound, rightBound;

        let direction = Adirection;

        if (direction = 0) {
            // Nothing to do
            return;
        }

        let leftBound = game.getLeftBound();
        let rightBound = game.getRightBound();

	if (direction = 1) {
            // Move left
            let x = x - 2;
	    if (x < leftBound) {
		let x = leftBound;
    	    }
            do Screen.setColor(false);
            do Screen.drawRectangle((x + width) + 1, y, (x + width) + 2, y + height);
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 1, y + height);
	} 
	else {
            // Move right
            let x = x + 2;
	    if ((x + width) > rightBound) 
            {
		let x = rightBound - width;
  	    } 
            do Screen.setColor(false);
            do Screen.drawRectangle(x - 2, y, x - 1, y + height);
            do Screen.setColor(true);
	    do Screen.drawRectangle((x + width) - 1, y, x + width, y + height);
	}

        return;
    }
}
