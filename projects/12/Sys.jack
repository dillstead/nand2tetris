/**
 * A library of basic system services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init();
        do Math.init();
        do Output.init();
        do Screen.init();
        do Keyboard.init();
        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts execution. */
    function void halt() {
        while (true) {
        }
        return;
    }

    /** Waits approximately duration milliseconds and then returns. */
    function void wait(int duration) {
        var int i, j;

        let i = 0;
        let j = 0;

        while (i < duration) {
            let j = 0;
            while (j < 95) {
                let j = j + 1;
            }
            let i = i + 1;
        }
        return;
    }

    /** Prints the given error code in the form "ERR<errorCode>", and halts. */
    function void error(int errorCode) {
        // 1 divide by zero
        // 2 negative square root
        // 3 out of memory
        // 4 out of screen bounds
        // 5 invalid rectangle coordinates
        // 6 invalid circle radius
        do Output.moveCursor(0, 0);
        do Output.printString("ERR");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}
